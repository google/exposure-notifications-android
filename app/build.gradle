apply plugin: 'com.android.application'

/*
 * Copyright 2020 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Fetch build number from command line arguments (for CI)
ext.build_number = project.hasProperty('BUILD_NUMBER') ? project.getProperty('BUILD_NUMBER') : "1"
ext.build_label = project.hasProperty('BUILD_LABEL') ? project.getProperty('BUILD_LABEL') : "SNAPSHOT"

// Fetch server details from command line arguments (for CI)
ext.key_server_upload_uri = project.hasProperty('UPLOAD_URI') ? project.getProperty('UPLOAD_URI') : "http://example.com"
ext.key_server_download_base_uri = project.hasProperty('DOWNLOAD_URI') ? project.getProperty('DOWNLOAD_URI') : "http://example.com"
ext.safetynet_api_key = project.hasProperty('SAFETYNET_KEY') ? project.getProperty('SAFETYNET_KEY') : "REPLACE-ME"

apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'com.google.android.apps.exposurenotification'

        minSdkVersion 23
        targetSdkVersion 29

        versionCode build_number.toInteger()
        versionName build_label

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true

        resValue("string", "key_server_upload_uri", key_server_upload_uri)
        resValue("string", "key_server_download_base_uri", key_server_download_base_uri)
        resValue("string", "safetynet_api_key", safetynet_api_key)
    }

    buildTypes {
        debug {
            versionNameSuffix ".debug"
            minifyEnabled false
            debuggable true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        debug {
            proto {
                srcDirs 'src/main/java/com/google/android/apps/exposurenotification/debug/proto'
            }
        }
        release {
            proto {
                srcDir 'src/main/java/com/google/android/apps/exposurenotification/debug/proto'
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        abortOnError false
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    annotationProcessor 'com.google.auto.value:auto-value:1.7'

    debugImplementation 'androidx.fragment:fragment-testing:1.2.4'

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha01'
    implementation 'androidx.concurrent:concurrent-futures:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.fragment:fragment:1.2.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-guava:2.2.5'
    implementation 'androidx.room:room-runtime:2.2.5'
    implementation 'androidx.work:work-runtime:2.3.4'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'com.google.android.gms:play-services-base:17.3.0'
    implementation 'com.google.android.gms:play-services-basement:17.3.0'
    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'
    implementation 'com.google.android.gms:play-services-tasks:17.1.0'
    implementation 'com.google.android.gms:play-services-vision:20.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.google.auto.value:auto-value-annotations:1.7'
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.protobuf:protobuf-java:3.11.4'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.2.4'
    implementation 'commons-io:commons-io:2.5'

    //noinspection FragmentGradleConfiguration
    testImplementation 'androidx.fragment:fragment-testing:1.2.4'
    testImplementation 'androidx.test.ext:junit:1.1.2-rc01'
    testImplementation 'androidx.test:core:1.3.0-rc01'
    testImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.google.guava:guava-testlib:29.0-jre'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'commons-io:commons-io:2.5'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
}
